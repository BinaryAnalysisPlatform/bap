

module Version = struct
  let find_dot s = String.index_opt s '.'

  let major v =
    match find_dot v with
    | None -> v
    | Some x -> String.sub v 0 x

  let strip_patch ver =
    match find_dot ver with
    | Some 2 ->  String.sub ver 0 4
    | _ -> String.sub ver 0 3

  let compare ver ver' =
    match find_dot ver, find_dot ver' with
    | Some x, Some y when x <> y ->
      compare (int_of_string (major ver)) (int_of_string (major ver'))
    | _ -> compare ver ver'

  let (<) x y = compare x y = -1
  let (>) x y = compare x y = 1
  let (=) x y = compare x y = 0
end

let llvm var () : unit =
  BaseEnv.var_define
    ~hide:true
    ~dump:true
    ~short_desc:(fun () -> "llvm-config --"^var)
    ("llvm_"^var)
    (fun () ->
       let llvm_config = BaseEnv.var_get "llvm_config" in
       let llvm_version = BaseEnv.var_get "llvm_version" in
       let link_mode =
         let llvm_static = BaseEnv.var_get "llvm_static" in
         if Version.(strip_patch llvm_version > "3.8") && llvm_static = "true"
         then "--link-static"
         else "" in
       let extract v =
	 OASISExec.run_read_one_line ~ctxt llvm_config [link_mode; "--"^v] in
       if Version.(strip_patch llvm_version > "3.4") && var = "ldflags"
       then extract var ^ " " ^ extract "system-libs"
       else extract var) |>
  definition_end

let llvm_version () : unit =
  BaseEnv.var_define
    ~hide:false
    ~dump:true
    ~cli:BaseEnv.CLIWith
    ~short_desc:(fun () -> "llvm version (e.g., 3.4)")
    "llvm_version"
    (fun () ->
       try
         ignore @@ OASISFileUtil.which ~ctxt "llvm-config";
         OASISExec.run_read_one_line ~ctxt "llvm-config" ["--version"]
       with Not_found -> "3.4") |>
  definition_end

let llvm_config () : unit =
  BaseEnv.var_define
    ~hide:false
    ~dump:true
    ~cli:BaseEnv.CLIWith
    ~short_desc:(fun () -> "llvm-config executable")
    "llvm_config"
    (fun () ->
       (* default macports if we're on mac os x *)
       let macosx = BaseEnv.var_get "system" = "macosx" in
       let vers = match BaseEnv.var_get "llvm_version" with
         | "" -> []
         | ver when macosx -> ["-mp-" ^ Version.strip_patch ver; "-" ^ Version.strip_patch ver]
         | ver -> ["-" ^ ver; "-" ^ Version.strip_patch ver] in
       find_map vers ~f:(fun ver ->
           try Some (OASISFileUtil.which ~ctxt ("llvm-config" ^ ver))
           with Not_found -> None) |> function
       | Some path -> path
       | None -> raise Not_found) |>
  definition_end

let llvm_mainlib () : unit =
  BaseEnv.var_define
    ~hide:true
    ~dump:true
    ~short_desc:(fun () -> "main LLVM library")
    "llvm_mainlib"
    (fun () -> "-lLLVM-"^BaseEnv.var_get "llvm_version") |>
  definition_end


let llvm_lib () : unit =
  BaseEnv.var_define
    ~hide:true
    ~dump:true
    ~short_desc:(fun () -> "LLVM library(ies) to link with")
    "llvm_lib"
    (fun () ->
       let llvm_static = BaseEnv.var_get "llvm_static" in
       let lib = if llvm_static = "true"
         then "llvm_libs"
         else "llvm_mainlib" in
       BaseEnv.var_get lib) |>
  definition_end

let () =
  define [
    llvm_version;
    llvm_config;
    llvm_mainlib;
    llvm "cxxflags";
    llvm "ldflags";
    llvm "cflags";
    llvm "libs";
    llvm_lib
  ]
